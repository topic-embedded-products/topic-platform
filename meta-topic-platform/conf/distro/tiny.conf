# Tiny distribution, using busybox for hotplug and login.
# Intended for small flash systems

DISTRO_VERSION = "topic-tiny.0"

# providers
PREFERRED_PROVIDER_libcrypto = "openssl"
PREFERRED_PROVIDER_libssl = "openssl"
PREFERRED_PROVIDER_jpeg = "libjpeg-turbo"
PREFERRED_PROVIDER_jpeg-native = "libjpeg-turbo-native"

VIRTUAL-RUNTIME_dev_manager = "busybox-mdev"
VIRTUAL-RUNTIME_login_manager = "busybox"
VIRTUAL-RUNTIME_init_manager = "${DISTRO_FEATURES_INITMAN}"
VIRTUAL-RUNTIME_initscripts = "initscripts"
# Avoid the "hwclock" script to be present in the filesystem.
# The kernel already takes care of the RTC and this script only
# breaks that functionality.
VIRTUAL-RUNTIME_base-utils-hwclock = ""

DISTRO_FEATURES_INITMAN = "sysvinit"
DISTRO_FEATURES_BACKFILL_CONSIDERED = "pulseaudio"
DISTRO_FEATURES = "ipv4 ipv6 usbgadget usbhost wifi bluetooth largefile ${DISTRO_FEATURES_LIBC}"

# don't run login on root console, messes with GUI
USE_VT = "0"
SYSVINIT_ENABLED_GETTYS = ""

# build some core libs with heavier compiler optimization
O3_OPT = "-O3 -pipe ${DEBUG_FLAGS}"
FULL_OPTIMIZATION:pn-libgcrypt = "${O3_OPT}"
FULL_OPTIMIZATION:pn-zlib = "${O3_OPT}"

# Allow licenses for gstreamer components (consult your legal department...)
LICENSE_FLAGS_ACCEPTED ?= "commercial"
COMMERCIAL_AUDIO_PLUGINS ?= "gst-plugins-ugly-mad gst-plugins-ugly-mpegaudioparse"
COMMERCIAL_VIDEO_PLUGINS ?= "gst-plugins-ugly-mpeg2dec gst-plugins-ugly-mpegstream gst-plugins-bad-mpegvideoparse"

# Package feed, defaults to build host's local (avahi) name
DISTRO_HOST_NAME ?= "${@os.uname()[1]}.local"
DISTRO_HOST_NAME_TOPICPLATFORM ?= "${DISTRO_HOST_NAME}"
DISTRO_FEED_NAME ?= "topic-platform"
DISTRO_FEED_PREFIX = "topic"
DISTRO_FEED_URI = "http://${DISTRO_HOST_NAME_TOPICPLATFORM}/${DISTRO_FEED_NAME}"
# Each machine should build its own package, otherwise all feeds will point
# to the sstate server.
DISTRO_HOST_NAME_TOPICPLATFORM[vardepvalue] = "${DISTRO_HOST_NAME_TOPICPLATFORM}"

